apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/checkstyle.gradle')

def STRING = "String"
def BOOLEAN = "boolean"
def INT = "int"

android {
  compileSdkVersion parent.ext.androidCompileSdkVersion

  defaultConfig {
    applicationId parent.ext.androidApplicationId
    minSdkVersion parent.ext.androidMinSdkVersion
    targetSdkVersion parent.ext.androidTargetSdkVersion
    versionCode parent.ext.androidVersionCode
    versionName parent.ext.androidVersionName

    buildConfigField INT, "CONCURRENT_INTERACTORS", "3"
    // If you have a dev/prod api url, you maybe need to set this in buildtypes
    buildConfigField STRING, "API_URL", '"http://api.randomuser.me/"'
    testInstrumentationRunner "me.panavtec.cleancontacts.CleanContactsRunner"
  }
  buildTypes {
    debug {
      testCoverageEnabled = true
      buildConfigField BOOLEAN, "RETROFIT_LOG", "true"
    }
    release {
      buildConfigField BOOLEAN, "RETROFIT_LOG", "false"

      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  flavorDimensions "type"
  productFlavors {
    mock {
      dimension "type"
      applicationId parent.ext.androidApplicationId + '.mock'
    }
  }

  //you can use the diamond operator, multi-catch, strings in switches
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
}

dependencies {
  implementation rootProject.ext.dependencies.androidSupportv7
  implementation rootProject.ext.dependencies.dagger
  annotationProcessor rootProject.ext.dependencies.daggerCompiler
  implementation rootProject.ext.dependencies.butterknife
  annotationProcessor rootProject.ext.dependencies.butterknife
  implementation rootProject.ext.dependencies.snackBars
  implementation rootProject.ext.dependencies.recyclerView
  implementation rootProject.ext.dependencies.picasso
  implementation rootProject.ext.dependencies.recyclerAdapters
  implementation rootProject.ext.dependencies.fAB
  implementation rootProject.ext.dependencies.coordinator
  annotationProcessor rootProject.ext.dependencies.coordinatorCompiler

  implementation project(':domain')
  implementation project(':presentation')
  implementation project(':data')

  androidTestAnnotationProcessor rootProject.ext.dependencies.daggerCompiler
  androidTestImplementation rootProject.ext.testDependencies.mockito
  androidTestImplementation rootProject.ext.androidTestDependencies.runner
  androidTestImplementation rootProject.ext.androidTestDependencies.rules
  androidTestImplementation rootProject.ext.androidTestDependencies.espresso_core
  androidTestImplementation (rootProject.ext.androidTestDependencies.espresso_contrib) {
    exclude module: 'support-annotations'
    exclude module: 'recyclerview-v7'
    exclude module: 'support-v4'
  }
  androidTestImplementation rootProject.ext.androidTestDependencies.espresso_intents

}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
  resolutionStrategy.force "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
}
